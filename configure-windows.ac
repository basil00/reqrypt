# configure-windows-cross.ac
# (C) 2011, all rights reserved,
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

AC_INIT(ReQrypt, 0)
AC_SUBST(PACKAGE_NAME_LONG, "ReQrypt")
AC_SUBST(PACKAGE_VERSION, `cat VERSION`)
AC_SUBST(PACKAGE_VERSION_SHORT, `cat VERSION`)

error=no

AC_PROG_CC([], [true], [error=yes])
AC_PROG_CC_C99

if test "$ac_cv_prog_cc_c99" = "no"
then
    AC_MSG_NOTICE([*** compiler does not support C99.])
    error=yes
fi

if test -d 'C:/Program Files/NSIS/'
then
    export PATH=$PATH:'/c/Program Files/NSIS/'
elif test -d 'C:/Program Files (x86)/NSIS/'
then
    export PATH=$PATH:'/c/Program Files (x86)/NSIS/'
fi

AC_PATH_PROG([NSIS_PATH], [makensis], [no])
if test "$NSIS_PATH" = "no"
then
    AC_MSG_NOTICE([*** NSIS is not installed.])
    error=yes
fi

AC_PATH_TOOL([WINDRES_PATH], [windres], [no])
if test "$WINDRES_PATH" = "no"
then
    AC_MSG_NOTICE([*** windres command not found.])
    error=yes
fi

if test "$DIVERT" = ""
then
    DIVERT="./"
fi
DIV=windiv  # Anything called 'divert' seems to break autoconf
ERT=ert
DIVERT_WORD=$DIV$ERT
DIVERT_NAME=WinDivert

AC_MSG_CHECKING([for $DIVERT_NAME files])

if test -e $DIVERT/x86/${DIVERT_NAME}32.sys -a \
        -e $DIVERT/x86/$DIVERT_NAME.dll -a \
        -e $DIVERT/amd64/${DIVERT_NAME}64.sys -a \
        -e $DIVERT/amd64/$DIVERT_NAME.dll -a \
        -e $DIVERT/include/$DIVERT_WORD.h
then
    AC_MSG_RESULT([yes])
else
    AC_MSG_RESULT([no])
    AC_MSG_NOTICE([*** $DIVERT_NAME files not found.])
    error=yes
fi

if test "$error" = "yes"
then
    AC_MSG_NOTICE([*** correct the above errors first ***])
    exit 1;
fi

case "$host" in
    amd64-*)
        AC_SUBST(BITS, 64)
        AC_SUBST(ARCH, amd64)
        ;;
    x86_64-*)
        AC_SUBST(BITS, 64)
        AC_SUBST(ARCH, amd64)
        ;;
    *)
        AC_SUBST(BITS, 32)
        AC_SUBST(ARCH, x86)
        ;;
esac

AC_SUBST(ROOT, `pwd`)
AC_SUBST(PLATFORM_CAPS, WINDOWS)
AC_SUBST(PLATFORM, windows)
AC_SUBST(CROSS, yes)
AC_SUBST(DIVERT_ROOT, `pwd`/$DIVERT)

AC_OUTPUT([cfg.mk src/cfg.h src/windows/install/install.nsi])

